import com.github.spotbugs.SpotBugsTask

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'info.solidsoft.pitest'
apply plugin: "com.github.spotbugs"


buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	dependencies {
		classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.0'
		classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:2.0.0"
	}
}


compileJava   {
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'
}

configurations {
	spotbugsStylesheets { transitive false }
}


pitest {
	targetClasses = ['pl.nosystems.java.datastructures.*']
	pitestVersion = '1.4.9'
	outputFormats = ['XML', 'HTML']
	timestampedReports = false
}


jacocoTestReport {
	reports {
		xml.enabled = true
		csv.enabled = true
		html.enabled = true
	}
}

jacoco {
	toolVersion = "0.8.4"
}

spotbugs {
	toolVersion '3.1.11'
	sourceSets = [sourceSets.main]
}

tasks.withType(SpotBugsTask) {
	reports.xml.enabled = false
	reports.html.enabled = true
	reports.html.stylesheet resources.text.fromArchiveEntry(configurations.spotbugsStylesheets, 'fancy-hist.xsl')
}

repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	testImplementation "junit:junit:4.12"
	testImplementation "org.mockito:mockito-core:2.23.0"

	spotbugsStylesheets "com.github.spotbugs:spotbugs:$spotbugs.toolVersion"
}
